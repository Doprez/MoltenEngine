struct SpriteData
{
	float4 uv;
	float4 col;
	float4 col2;
    float2 thickness;
    float2 data;

	float2 pos;
	float2 size;

	float2 origin;
	float rotation;
	float arraySlice;
};

struct VS_GS
{
    uint vID        : VERTEXID;
};

struct PS_IN
{
	float4 pos      : SV_POSITION;
	float3 uv       : TEXCOORD0;
    float2 uvLocal  : TEXCOORD1;      //UV Relative to top left of sprite (0 - 1 range)
    uint id         : VERTEXID;
};

StructuredBuffer<SpriteData> spriteData : register(t0);

Texture2DArray mapDiffuse : register(t1);
Texture2DMSArray<float4> mapDiffuseMS : register(t2);
SamplerState diffuseSampler;

float4x4 wvp;
float2 textureSize;
uint sampleCount = 8;
uint vertexOffset = 0;

static float degToRad360 = 6.28319;

VS_GS VS(uint vID : SV_VertexID)
{
    VS_GS output;

    output.vID = vID;
	return output;
}

const static float2 spriteCorners[4] = {
	float2(0,-1),
	float2(0, 0),
	float2(1,-1),
	float2(1,0),
};

// 0 = left, 1 = top, 2 = right, 3 = bottom
const static int2 uvTable[4] = {
	int2(0,3), // Bottom left
	int2(0,1), // Top left
	int2(2,3), // Bottom right
	int2(2,1), // Top right
};

float4 baseUV = float4(0,0,1,1);
static const float2 QUAD_CENTER_UV = float2(0.5,0.5);
static const float PI = 3.14159265;

float2x2 GetRotation(float angle)
{
	// Compute a 2x2 rotation matrix.
	float c = cos(angle);
	float s = sin(angle);

	return float2x2(c, -s, s, c);
}

[maxvertexcount(4)]
void GS(point VS_GS input[1], uint pID : SV_PrimitiveID, inout TriangleStream<PS_IN> spriteStream)
{
	PS_IN v;
    v.id = vertexOffset + input[0].vID;
    SpriteData sv = spriteData[v.id];
    float4 uv = sv.uv;

    uv.xz /= textureSize.x; // scale left (x) and right(z) to 0 - 1.0 range.
	uv.yw /= textureSize.y; // Scale top (y) and bottom(w) to 0 - 1.0 range.

	float2x2 rot = GetRotation(sv.rotation);
	float2 origin = float2(sv.origin.x, -sv.origin.y);  // Invert y origin
	float2 pos = float2(sv.pos.x, -sv.pos.y);           // Invert y axis
	float2 p;

	[unroll]
	for (int i = 0; i < 4; i++)
	{
		p = mul(sv.size.xy * (spriteCorners[i] - origin), rot);
		p += pos;
		v.pos = float4(p, 0, 1);
		v.pos = mul(v.pos, wvp);
		v.uv.x = uv[uvTable[i].x];
		v.uv.y = uv[uvTable[i].y];
		v.uv.z = sv.arraySlice;
        v.uvLocal.x = baseUV[uvTable[i].x];
        v.uvLocal.y = baseUV[uvTable[i].y];
		spriteStream.Append(v);
	}
}

float isRectBorder(float2 uv, float2 thickness)
{
    float left = step(uv.x, thickness.x);
    float right = step(1 - thickness.x, uv.x);
    float top = step(uv.y, thickness.y);
    float bottom = step(1 - thickness.y, uv.y);

    return step(1, left + right + top + bottom);
}

float4 PS( PS_IN input ) : SV_Target
{
    SpriteData sd = spriteData[input.id];

    float isBorder = isRectBorder(input.uvLocal, sd.thickness);

	float4 col = mapDiffuse.Sample(diffuseSampler, input.uv) * sd.col;
	return (col * (1 - isBorder)) + (sd.col2 * isBorder);
}

float4 PS_NoTexture(PS_IN input) : SV_Target
{
    SpriteData sd = spriteData[input.id];
    float isBorder = isRectBorder(input.uvLocal, sd.thickness);
	return (sd.col * (1 - isBorder)) + (sd.col2 * isBorder);
}

float4 PS_Circle(PS_IN input) : SV_Target
{   
    // Data.x (D1) = Angle range (Radians)

    float4 col = spriteData[input.id].col;
    float4 col2 = spriteData[input.id].col2;

    float2 d = input.uvLocal - 0.5;
    float rad = -PI + spriteData[input.id].data.x;
    if(rad < atan2(d.y, d.x))
        discard;

    float halfThick = spriteData[input.id].thickness.x / 2;
    float thickCenter = 0.5 - halfThick;
    float thickStart = thickCenter - halfThick;
    float thickEnd = thickCenter + halfThick;

    float dist = length(distance(input.uvLocal, QUAD_CENTER_UV)); // Distance from quad center (0.5, 0.5)
    float innerBorder = smoothstep(thickStart, thickCenter, dist);
    float outerBorder = min(0.5, 0.5 - smoothstep(thickCenter, thickEnd, dist)) / 0.5;
    float border = step(thickStart, innerBorder) * step(outerBorder, thickEnd);
    float isBorder = step(thickCenter, dist);

    float inner = 1 - smoothstep(0.97, 1, (dist / 0.5));

    float4 sampCol = mapDiffuse.Sample(diffuseSampler, input.uv);
    float4 finalCol = sampCol * col;
    float4 borderCol = float4(col2.rgb, col2.a * innerBorder + (border * outerBorder));

    return (float4(finalCol.rgb, finalCol.a * inner ) * (1-isBorder)) + (borderCol * isBorder);  
}

float4 PS_Circle_NoTex(PS_IN input) : SV_Target
{       
    // Data.x (D1) = Angle range (Radians)

    float4 col = spriteData[input.id].col;
    float4 col2 = spriteData[input.id].col2;

    float2 d = input.uvLocal - 0.5;
    float rad = -PI + spriteData[input.id].data.x;
    if(rad < atan2(d.y, d.x))
        discard;

    float halfThick = spriteData[input.id].thickness / 2;
    float thickCenter = 0.5 - halfThick;
    float thickStart = thickCenter - halfThick;
    float thickEnd = thickCenter + halfThick;

    float dist = length(distance(input.uvLocal, QUAD_CENTER_UV)); // Distance from quad center (0.5, 0.5)
    float innerBorder = smoothstep(thickStart, thickCenter, dist);
    float outerBorder = min(0.5, 0.5 - smoothstep(thickCenter, thickEnd, dist)) / 0.5;
    float border = step(thickStart, innerBorder) * step(outerBorder, thickEnd);
    float isBorder = step(thickCenter, dist);

    float inner = 1 - smoothstep(0.97, 1, (dist / 0.5));

    float4 borderCol = float4(col2.rgb, col2.a * outerBorder);
    float4 innerCol = float4(col.rgb, col.a * inner );

    return lerp(innerCol, borderCol, isBorder);
}

float4 PS_Line(PS_IN input) : SV_Target
{  
    float dist = abs(input.uvLocal.y - 0.5);
    float step = 1.0 - smoothstep(0.1, spriteData[input.id].thickness.y, dist);

    float4 col1 = spriteData[input.id].col;
    float4 col2 = spriteData[input.id].col2;
    float4 col = lerp(col1, col2, input.uvLocal.x);

    return float4(col.rgb, col.a * step);
}

float4 PS_Grid( PS_IN input ) : SV_Target
{
    // Data.x (D1) = Cell increment X
    // Data.y (D2) = Cell increment Y

    SpriteData sd = spriteData[input.id];
    float2 cellSize = sd.data.xy;
    float thickness = sd.thickness;
    float result;

    for(float ix = 0.0; ix < 1.0; ix += cellSize.x)
    {
        result += 1.0 - smoothstep(0.0, thickness, abs(input.uvLocal.x - ix));
    }

    for(float iy = 0.0; iy < 1.0; iy += cellSize.y)
    {
        result += 1.0 - smoothstep(0.0, thickness, abs(input.uvLocal.y - iy));
    }

	return (sd.col * (1-result)) + (sd.col2 * result);
}