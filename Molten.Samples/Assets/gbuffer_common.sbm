//GBuffer Vertex shader output.
struct VS_OUT
{
	float4 pos			: SV_POSITION;
	float2 uv			: TEXCOORD0;
	float3 normal		: NORMAL0;
	float3 tangent		: TANGENT0;
	float3 binormal		: BINORMAL0;
};

//GBuffer Vertex shader output, without color
struct VS_OUTNOCOL
{
	float4 pos			: SV_POSITION;
	float2 uv			: TEXCOORD0;
	float3 normal		: NORMAL0;
	float3 tangent		: TANGENT0;
	float3 binormal		: BINORMAL0;
};

struct PS_OUT
{
	float4 diffuse		: SV_Target0;
	float3 normal		: SV_Target1;
	float4 emissive		: SV_Target2;
};

//Variables used across multiple light shaders.
cbuffer commonLight : register(b0)
{
	float4x4 InvertViewProjection;
}

cbuffer commonGBuffer : register(b0)
{
	float4x4 world;
	float4x4 view;
	float4x4 projection;
}

cbuffer cbGBufferPixel : register(b1)
{
	float EmissivePower : packoffset(c0);
}

SamplerState texSampler : register(s1);
Texture2D<float4> mapDiffuse	: register(t0);
Texture2D<float3> mapNormal		: register(t1);
Texture2D<float4> mapGlow		: register(t2);
Texture2D<float> mapDepth		: register(t3);

struct MATERIAL{
	float3 colorData;
	float4 worldPos;
	float3 normal;
	float2 uv;
	float depth;
};

MATERIAL UnpackGBuffer(float3 screenPos){
	MATERIAL o = (MATERIAL)0;

	// Obtain screen position
	screenPos.xy /= screenPos.z;

	// Obtain textureCoordinates corresponding to the current pixel
	// The screen coordinates are in [-1,1]*[1,-1]
	// The texture coordinates need to be in [0,1]*[0,1]
	o.uv = 0.5f * (float2(screenPos.x, -screenPos.y) + 1);

	// Get color and normal
	float4 colorData = mapDiffuse.Sample(texSampler, o.uv);
	float3 normalData = mapNormal.Sample(texSampler, o.uv);

	// Tranform normal back into [-1,1] range
	o.normal = 2.0f * normalData.xyz - 1.0f;

	o.colorData = colorData.rgb;

	// UNUSED
	// colorData.a
	// emissive.a
	// there is no normal.a because of 11,11,10 bit channels.

	// Read depth
	o.depth = mapDepth.Sample(texSampler, o.uv).r;

	// Compute screen-space position
	o.worldPos.xy = screenPos.xy;
	o.worldPos.z = o.depth;
	o.worldPos.w = 1.0f;

	// Transform to world space
	o.worldPos = mul(o.worldPos, InvertViewProjection);
	o.worldPos /= o.worldPos.w;

	return o;
}